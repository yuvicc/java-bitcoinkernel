plugins {
    id 'java'
    id 'application'
}

dependencies {
    // Testing
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

repositories {
    mavenCentral()
}

// Define paths for Bitcoin Core
def bitcoinCoreDir = file("${project.projectDir}/bitcoinkernel/bitcoin")
def bitcoinBuildDir = file("${bitcoinCoreDir}/build")
def bitcoinKernelHeader = file("${bitcoinCoreDir}/src/kernel/bitcoinkernel.h")

// Task to build Bitcoin Core with kernel library
tasks.register('buildBitcoinCore', Exec) {
    workingDir bitcoinCoreDir

    // Configure Bitcoin Core build (matching rust-bitcoinkernel approach)
    commandLine 'cmake', '-B', 'build',
            '-DCMAKE_BUILD_TYPE=RelWithDebInfo',
            '-DBUILD_KERNEL_LIB=ON',
            '-DBUILD_TESTS=OFF',
            '-DBUILD_KERNEL_TEST=OFF',
            '-DBUILD_TX=OFF',
            '-DBUILD_WALLET_TOOL=OFF',
            '-DENABLE_WALLET=OFF',
            '-DENABLE_EXTERNAL_SIGNER=OFF',
            '-DBUILD_UTIL=OFF',
            '-DBUILD_BITCOIN_BIN=OFF',
            '-DBUILD_DAEMON=OFF',
            '-DBUILD_UTIL_CHAINSTATE=OFF',
            '-DBUILD_CLI=OFF',
            '-DBUILD_SHARED_LIBS=ON',
            '-DCMAKE_INSTALL_LIBDIR=lib',
            '-DENABLE_IPC=OFF'

    // Only run if build directory doesn't exist or header has changed
//    inputs.file bitcoinKernelHeader
//    inputs.dir "${bitcoinCoreDir}/src"
//    outputs.dir bitcoinBuildDir
}

// Task to compile the built Bitcoin Core
tasks.register('compileBitcoinCore', Exec) {
    dependsOn buildBitcoinCore
    workingDir bitcoinCoreDir

    // todo: add more robust approach here for compiling parallelly, for now I have kept it low(we can use nproc here but it crashes my machine)
    commandLine 'cmake', '--build', 'build', '-j4'

//    inputs.dir bitcoinBuildDir
//    outputs.files(
//            "${bitcoinBuildDir}/src/kernel/libbitcoinkernel.so",
//    )
}
compileBitcoinCore.dependsOn(buildBitcoinCore)

// Configure source sets
sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
    }
}

application {
    mainClass = "org.bitcoinkernel.TestKernel"
    applicationDefaultJvmArgs = [
            "--enable-native-access=ALL-UNNAMED"
    ]
}

// Ensure proper build order
compileJava.dependsOn compileBitcoinCore

// Configure test task
test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }

    // Set library path for native library loading (must be absolute path)
    def libPath = file("${bitcoinCoreDir}/build/lib").absolutePath

    // Set java.library.path via JVM args (more reliable than systemProperty)
    jvmArgs "-Djava.library.path=${libPath}"

    // Enable native access for Foreign Function & Memory API
    jvmArgs "--enable-native-access=ALL-UNNAMED"

    // Set LD_LIBRARY_PATH environment variable
    environment "LD_LIBRARY_PATH", libPath

    // Ensure dependency on Bitcoin Core compilation
    dependsOn compileBitcoinCore
}
